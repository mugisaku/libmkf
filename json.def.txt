json = element;


element =
   fp_number_form0|
   fp_number_form1|
  positive_integer|
  negative_integer|
            object|
             array|
              null|
              true|
             false|
            string;


null  = "null";
true  = "true";
false = "false";


object = "{":[object_member:{",":object_member}]:"}";

object_member = string:":":element;

array = "[":[element:{",":element}]:"]";


fp_number_form0 = integer:".":CTYPE_digit:{CTYPE_digit}:[exponent];
fp_number_form1 = integer:exponent;


exponent = ("e"|"E"):["+"|"-"]:CTYPE_digit:{CTYPE_digit};


integer = positive_integer|
          negative_integer;

positive_integer = "0"|natural_number;
negative_integer = "-":natural_number;


natural_number = one_to_nine:{CTYPE_digit};
one_to_nine = "1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9";


string = "\"":{string_element}:"\"";

string_element =
                     " "|
             CTYPE_alnum|
           CTYPE_unicode|
                     "!"|
                     "#"|
                     "$"|
                     "%"|
                     "&"|
                     "("|
                     ")"|
                     "="|
                     "~"|
                     "^"|
                     "|"|
                     "@"|
                     "["|
                     "]"|
                     "{"|
                     "}"|
                     ":"|
                     ";"|
                     "*"|
                     "+"|
                     "-"|
                     "`"|
                     "<"|
                     ">"|
                     "?"|
                     "_"|
                     ","|
                     "."|
                    "\'"|
           tab_character|
       newline_character|
        return_character|
         slash_character|
     backslash_character|
      formfeed_character|
     backspace_character|
  double_quote_character;


newline_character      = "\\":"n";
return_character       = "\\":"r";
tab_character          = "\\":"t";
backspace_character    = "\\":"b";
formfeed_character     = "\\":"f";
slash_character        = "\\":"/";
backslash_character    = "\\":"\\";
double_quote_character = "\\":"\"";





